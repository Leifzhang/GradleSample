// Top-level build file where you can add configuration options common to all sub-projects/modules.


buildscript {
    ext.kotlin_version = "1.7.10"
    repositories {
        maven { setUrl("https://maven.aliyun.com/repository/central/") }
        maven {
            setUrl("https://storage.googleapis.com/r8-releases/raw")
        }

        google()
    }
    dependencies {
        classpath("com.android.tools:r8:3.2.60")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
plugins {
    id("kronos.dependencies") apply(false)
    id("kronos.library") apply(false)
    id('com.android.application') version '7.2.2' apply false
    id('com.android.library') version '7.2.2' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.7.10' apply false
    id("kronos.a8") apply(false)
}


allprojects {
    apply from: new File(getRootDir(), ".buildscripts/flavor.gradle")
    group = "com.kronos.sample"
    configurations.all { Configuration c ->
        c.resolutionStrategy {
            dependencySubstitution {
                all { DependencySubstitution dependency ->
                    if (dependency.requested instanceof ModuleComponentSelector) {
                        def p = rootProject.allprojects.find { p -> p.group == dependency.requested.group && p.name == dependency.requested.module }
                        if (p != null) {
                            logger.lifecycle(project(p.path).name + ":selected local project")
                            dependency.useTarget(project(p.path), 'selected local project')
                        }
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
